#pragma enable_d3d11_debug_symbols

int CentersCount;

StructuredBuffer<float2> CentersPositions;
StructuredBuffer<float> AdditiveCoefficients;
StructuredBuffer<float> MultiplicativeCoefficients;

RWTexture3D<float> MuGrids;
RWTexture2D<float> PsiGrid;

float GradientLambdaStep;

float GradientEpsilon;

float2 GridSize;
float2 MinCorner;
float2 Diff;

RWStructuredBuffer<int> StopConditions;

float EuclideanNorm(float2 point1, float2 point2)//TODO to common utils
{
	return distance(point1, point2);
}

#pragma kernel ZeroIterationInit
[numthreads(8,8,1)]
void ZeroIterationInit(uint3 id : SV_DispatchThreadID)
{
	uint2 texturePoint = id.xy;
	uint centerIndex = id.z;
	
	PsiGrid[texturePoint] = 1;
	
	float muRatio = 1.0 / CentersCount;
	MuGrids[id] = muRatio;

	//MuGrids[uint3(texturePoint, 0)] = 0.1;
	//MuGrids[uint3(texturePoint, 1)] = 0.9;
}

#pragma kernel UpdateMu
[numthreads(8,8,1)]
void UpdateMu(uint3 id : SV_DispatchThreadID)
{
	uint2 texturePoint = id.xy;
	uint centerIndex = id.z;
	
	float a = AdditiveCoefficients[centerIndex];
	float w = MultiplicativeCoefficients[centerIndex];
	float2 centerPosition = CentersPositions[centerIndex];

	float2 pointRatio = float2(texturePoint.x, texturePoint.y) / GridSize;
	float2 spacePoint = MinCorner + Diff * pointRatio;
	
	float densityValue = 1;
	float m = 2;
	
	float distance = EuclideanNorm(centerPosition, spacePoint);

	float psiValue = PsiGrid[texturePoint];

	float newMuValue = -psiValue / (m * densityValue * (distance / w + a));

	float normalizedMuValue;

	if(newMuValue >= 0 && newMuValue <= 1)
	{
		normalizedMuValue = newMuValue;
	}
	else
	{
		float previousMuValue = MuGrids[id];
		float muGradient = psiValue + m * previousMuValue * (distance / w + a) * densityValue;
		float signMuGradient = sign(muGradient);
		normalizedMuValue = 0.5 * (1 - signMuGradient);
	}

	MuGrids[id] = normalizedMuValue;
}

#pragma kernel UpdatePsi
[numthreads(8,8,1)]
void UpdatePsi(uint3 id : SV_DispatchThreadID)
{
	uint2 texturePoint = id.xy;
	float grad = 0;
	for(int centerIndex = 0; centerIndex < CentersCount; centerIndex++)
		grad += MuGrids[uint3(texturePoint, centerIndex)];

	grad -= 1;

	PsiGrid[texturePoint] = PsiGrid[texturePoint] + GradientLambdaStep * grad;

	//check psi gradient value
	//if(grad > GradientEpsilon)//reset if gradient too big
	{
		StopConditions[0] = 0;
	}
}